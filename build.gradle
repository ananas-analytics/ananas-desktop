def String getSystemProperty(String propertyName, String defaultValue)
{
	return System.properties.getProperty(propertyName, defaultValue)
}

def String getAnanasEnv() {
	return getSystemProperty('ANANAS_ENV', 'development')
}

def getPackrPlatform() {
	switch(project.platform) {
		case 'linux':
			return 'linux64'
		case 'mac':
			return 'mac'
		case 'win':
			return 'windows64'
		default: 
			return 'linux64'
	}
}


if (!project.hasProperty('platform')) {
	project.ext.platform = ''	
}

if (!project.hasProperty('JDK_HOME')) {
	project.ext.JDK_HOME = ''
}

task printVersion {
	doLast {
		println "Version: $project.version; Runner name: $project.runnerJarName"
	}
}

task copyRunner(type: Copy) {
	dependsOn ":runner:shadowJar"

	from file("runner/build/libs/${project.runnerJarName}.jar")

	into file("ui/resource")
}

task packRunner(type: Exec) {
	dependsOn ":ui:cleanRunnerBinary"
	dependsOn ":runner:clean"
	dependsOn ":runner:shadowJar"

	println "Packing settings: JDK_HOME=[${project.JDK_HOME}] for platform [${project.platform}]"

    println "Jar: ${project.runnerJarName}"

	workingDir "."

	commandLine "java", "-jar", "buildSrc/src/main/resources/packr.jar",
		"--platform", "${getPackrPlatform()}",
		"--jdk", "$project.JDK_HOME",
		"--executable", "ananas",
		"--classpath", "runner/build/libs/${project.runnerJarName}.jar",
		"--mainclass", "org.ananas.runner.api.Main",
		"--vmargs", "Xmx4G",
		"--output", "ui/resources/runner"
}

task pack(type: Exec) {
	dependsOn ':ui:build'
	dependsOn packRunner

	inputs.property 'NODE_ENV', getAnanasEnv()

	println "Binary version: ${project.version}"

	println "Environment: ${getAnanasEnv()}"

	workingDir "./ui"

	environment "NODE_ENV", "${getAnanasEnv()}"

	commandLine "./node_modules/.bin/electron-builder", "--${project.platform}"
}

